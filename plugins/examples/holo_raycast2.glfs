#version 430

//***** begin interface of view.glsl ***********************************
mat4 get_modelview_matrix();
mat4 get_projection_matrix();
mat4 get_inverse_projection_matrix();
mat4 get_modelview_projection_matrix();
mat4 get_inverse_modelview_matrix();
mat4 get_inverse_modelview_projection_matrix();
mat3 get_normal_matrix();
mat3 get_inverse_normal_matrix();
//***** end interface of view.glsl ***********************************

//***** begin interface of fragment.glfs ***********************************
void finish_fragment(vec4 color);
void finish_fragment(vec4 color, float depth);
//***** end interface of fragment.glfs ***********************************

// looking Glass calibration values
uniform float pitch  = 673.46088569750157;
uniform float slope  = -0.074780801514116493;
uniform float center = 0.076352536678314209;

// stereoscopy parameters
uniform vec2 viewport_dims;
uniform vec3 eye_pos;
uniform float eye_separation;
uniform float screen_width;
uniform float screen_height;
uniform float parallax_zero_depth;
uniform float z_near;
uniform float z_far;

uniform vec3 light_direction;
uniform bool enable_shadows;
// our test parameters (TODO: use proper named uniforms)
uniform vec4 test;

// width of one subpixel as a fraction of full viewport width
const float subpixel_width = 1./(3*viewport_dims.x);

// input from screen_quad vertex shader (we ignore this)
in vec2 texcoord_fs;


// demo from https://www.shadertoy.com/view/ttXSDN
//float screenSize = 2.0; // Just do everything in screenSizes

// The MIT License
// Copyright ï¿½ 2019 Inigo Quilez
// Permission is hereby granted, free of charge, to any person obtaining a copy of this software and associated documentation files (the "Software"), to deal in the Software without restriction, including without limitation the rights to use, copy, modify, merge, publish, distribute, sublicense, and/or sell copies of the Software, and to permit persons to whom the Software is furnished to do so, subject to the following conditions: The above copyright notice and this permission notice shall be included in all copies or substantial portions of the Software. THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE.


// A filtered xor pattern. Since an xor patterns is a series of checkerboard
// patterns, I derived this one from https://www.shadertoy.com/view/XlcSz2

// Other filterable simple procedural patterns:
//
// checker, 2D, box filter: https://www.shadertoy.com/view/XlcSz2
// checker, 3D, box filter: https://www.shadertoy.com/view/XlXBWs
// checker, 3D, tri filter: https://www.shadertoy.com/view/llffWs
// grid,    2D, box filter: https://www.shadertoy.com/view/XtBfzz
// xor,     2D, box filter: https://www.shadertoy.com/view/tdBXRW
//
// Article: https://iquilezles.org/articles/filterableprocedurals


// --- analytically box-filtered grid ---

const float N = 2.0; // grid ratio
float gridTextureGradBox( in vec2 p, in vec2 ddx, in vec2 ddy )
{
	// filter kernel
	vec2 w = max(abs(ddx), abs(ddy)) + 0.01;

	// analytic (box) filtering
	vec2 a = p + 0.5*w;                        
	vec2 b = p - 0.5*w;           
	vec2 i = (floor(a)+min(fract(a)*N,1.0)-
				floor(b)-min(fract(b)*N,1.0))/(N*w);
	//pattern
	//return (1.0-i.x)*(1.0-i.y);
	return 1.0-i.x-i.y+2.0*i.x*i.y;
}

// --- unfiltered grid ---

float gridTexture( in vec2 p )
{
	// coordinates
	vec2 i = step( fract(p), vec2(1.0/N) );
	//pattern
	//return (1.0-i.x)*(1.0-i.y);   // grid (N=10)

	// other possible patterns are these
	//return 1.0-i.x*i.y;           // squares (N=4)
	return 1.0-i.x-i.y+2.0*i.x*i.y; // checker (N=2)
}

//===============================================================================================
//===============================================================================================
// ray-object intersection routines (from: https://iquilezles.org/articles/intersectors/)
//===============================================================================================
//===============================================================================================

float plane_intersection(in vec3 ro, in vec3 rd, in vec4 p) {
    return -(dot(ro,p.xyz)+p.w)/dot(rd,p.xyz);
}

vec2 box_intersection(in vec3 ro, in vec3 rd, in vec3 box_pos, in vec3 box_size, out vec3 outNormal) {
	ro -= box_pos;
    vec3 m = 1.0/rd; // can precompute if traversing a set of aligned boxes
    vec3 n = m*ro;   // can precompute if traversing a set of aligned boxes
    vec3 k = abs(m)*box_size;
    vec3 t1 = -n - k;
    vec3 t2 = -n + k;
    float tN = max( max( t1.x, t1.y ), t1.z );
    float tF = min( min( t2.x, t2.y ), t2.z );
    if( tN>tF || tF<0.0) return vec2(-1.0); // no intersection
    outNormal = (tN>0.0) ? step(vec3(tN),t1) : // ro ouside the box
                           step(t2,vec3(tF));  // ro inside the box
    outNormal *= -sign(rd);
    return vec2( tN, tF );
}

vec2 sphere_intersection(in vec3 ro, in vec3 rd, in vec3 center, float rad, out vec3 outNormal) {
    vec3 oc = ro - center;
    float b = dot( oc, rd );
    float c = dot( oc, oc ) - rad*rad;
    float h = b*b - c;
    if( h<0.0 ) return vec2(-1.0); // no intersection
    h = sqrt( h );
	vec3 pos = ro + (-b-h) * rd;
	outNormal = normalize(pos - center);
    return vec2( -b-h, -b+h );
}

float cylinder_intersection(in vec3 ro, in vec3 rd, in vec3 a, in vec3 b, float rad, out vec3 outNormal) {
    vec3  ba = b  - a;
    vec3  oc = ro - a;
    float baba = dot(ba,ba);
    float bard = dot(ba,rd);
    float baoc = dot(ba,oc);
    float k2 = baba            - bard*bard;
    float k1 = baba*dot(oc,rd) - baoc*bard;
    float k0 = baba*dot(oc,oc) - baoc*baoc - rad*rad*baba;
    float h = k1*k1 - k2*k0;
    if( h<0.0 ) return -1.0;//no intersection
    h = sqrt(h);
    float t = (-k1-h)/k2;
    // body
    float y = baoc + t*bard;
    if( y>0.0 && y<baba ) {
		outNormal = (oc+t*rd - ba*y/baba)/rad;
		return t;
	}
    // caps
    t = ( ((y<0.0) ? 0.0 : baba) - baoc)/bard;
    if( abs(k1+k2*t)<h )
    {
		outNormal = ba*sign(y)/sqrt(baba);
        return t;
    }
    return -1.0;//no intersection
}

float torus_intersection(in vec3 ro, in vec3 rd, in vec3 pos, in vec2 tor) {
	ro -= pos;

    float po = 1.0;
    float Ra2 = tor.x*tor.x;
    float ra2 = tor.y*tor.y;
    float m = dot(ro,ro);
    float n = dot(ro,rd);
    float k = (m + Ra2 - ra2)/2.0;
    float k3 = n;
    float k2 = n*n - Ra2*dot(rd.xy,rd.xy) + k;
    float k1 = n*k - Ra2*dot(rd.xy,ro.xy);
    float k0 = k*k - Ra2*dot(ro.xy,ro.xy);
    
    if( abs(k3*(k3*k3-k2)+k1) < 0.01 )
    {
        po = -1.0;
        float tmp=k1; k1=k3; k3=tmp;
        k0 = 1.0/k0;
        k1 = k1*k0;
        k2 = k2*k0;
        k3 = k3*k0;
    }
    
    float c2 = k2*2.0 - 3.0*k3*k3;
    float c1 = k3*(k3*k3-k2)+k1;
    float c0 = k3*(k3*(c2+2.0*k2)-8.0*k1)+4.0*k0;
    c2 /= 3.0;
    c1 *= 2.0;
    c0 /= 3.0;
    float Q = c2*c2 + c0;
    float R = c2*c2*c2 - 3.0*c2*c0 + c1*c1;
    float h = R*R - Q*Q*Q;
    
    if( h>=0.0 )  
    {
        h = sqrt(h);
        float v = sign(R+h)*pow(abs(R+h),1.0/3.0); // cube root
        float u = sign(R-h)*pow(abs(R-h),1.0/3.0); // cube root
        vec2 s = vec2( (v+u)+4.0*c2, (v-u)*sqrt(3.0));
        float y = sqrt(0.5*(length(s)+s.x));
        float x = 0.5*s.y/y;
        float r = 2.0*c1/(x*x+y*y);
        float t1 =  x - r - k3; t1 = (po<0.0)?2.0/t1:t1;
        float t2 = -x - r - k3; t2 = (po<0.0)?2.0/t2:t2;
        float t = 1e20;
        if( t1>0.0 ) t=t1;
        if( t2>0.0 ) t=min(t,t2);
        return t;
    }
    
    float sQ = sqrt(Q);
    float w = sQ*cos( acos(-R/(sQ*Q)) / 3.0 );
    float d2 = -(w+c2); if( d2<0.0 ) return -1.0;
    float d1 = sqrt(d2);
    float h1 = sqrt(w - 2.0*c2 + c1/d1);
    float h2 = sqrt(w - 2.0*c2 - c1/d1);
    float t1 = -d1 - h1 - k3; t1 = (po<0.0)?2.0/t1:t1;
    float t2 = -d1 + h1 - k3; t2 = (po<0.0)?2.0/t2:t2;
    float t3 =  d1 - h2 - k3; t3 = (po<0.0)?2.0/t3:t3;
    float t4 =  d1 + h2 - k3; t4 = (po<0.0)?2.0/t4:t4;
    float t = 1e20;
    if( t1>0.0 ) t=t1;
    if( t2>0.0 ) t=min(t,t2);
    if( t3>0.0 ) t=min(t,t3);
    if( t4>0.0 ) t=min(t,t4);
    return t;
}

vec3 torus_normal(in vec3 pos, vec2 tor) {
    return normalize( pos*(dot(pos,pos)-tor.y*tor.y - tor.x*tor.x*vec3(1.0,1.0,-1.0)));
}

//===============================================================================================
//===============================================================================================
// scene
//===============================================================================================
//===============================================================================================

// objects
const vec4 back_plane = vec4(0.0, 0.0, -1.0, -1.0);

const vec3 floor_box_pos = vec3(0.0, -1.0, 0.0);
const vec3 floor_box_ext = vec3(1.0, 0.1, 1.0);
const vec3 pillar_box_pos = vec3(0.0, 0.0, -0.9);
const vec3 pillar_box_ext = vec3(0.1, 1.0, 0.1);
const vec3 beam_box_pos = vec3(0.0, 0.9, 0.0);
const vec3 beam_box_ext = vec3(0.1, 0.1, 1.0);

const float rope_radius = 0.05;
const vec3 rope0_cyl_start = vec3(0.0, 0.9, 0.0);
const vec3 rope0_cyl_end = vec3(0.0, 0.0, 0.0);
const vec3 rope1_cyl_start = vec3(0.0, 0.9, 0.9);
const vec3 rope1_cyl_end = vec3(0.0, 0.3, 0.9);

const vec3 torus_pos = vec3(0.0, 0.0, 0.9);
const vec2 torus_params = vec2(0.3, 0.075);

const vec4 sph = vec4(0.0, 0.0, 0.0, 0.25);

float intersect( vec3 ro, vec3 rd, out vec3 pos, out vec3 nor, out int matid )
{
	// raytrace
	float tmin = 1000000.0;
	nor = vec3(0.0);
	pos = ro + tmin*rd;
	matid = -1;

	// raytrace floor box
	vec3 nml;
	float h = box_intersection(ro, rd, floor_box_pos, floor_box_ext, nml).x;
	if(h > 0.0 && h < tmin) {
		tmin = h;
		pos = ro + h*rd;
		nor = nml;
		matid = 0;
	}

	// raytrace pillar box
	h = box_intersection(ro, rd, pillar_box_pos, pillar_box_ext, nml).x;
	if(h > 0.0 && h < tmin) {
		tmin = h;
		pos = ro + h*rd;
		nor = nml;
		matid = 1;
	}

	// raytrace beam box
	h = box_intersection(ro, rd, beam_box_pos, beam_box_ext, nml).x;
	if(h > 0.0 && h < tmin) {
		tmin = h;
		pos = ro + h*rd;
		nor = nml;
		matid = 1;
	}

	// raytrace rope cylinders
	h = cylinder_intersection(ro, rd, rope0_cyl_start, rope0_cyl_end, rope_radius, nml);
	if( h>0.0 && h<tmin ) {
		tmin = h;
		pos = ro + h*rd;
		nor = nml;
		matid = 2;
	}

	h = cylinder_intersection(ro, rd, rope1_cyl_start, rope1_cyl_end, rope_radius, nml);
	if( h>0.0 && h<tmin ) {
		tmin = h;
		pos = ro + h*rd;
		nor = nml;
		matid = 2;
	}

	// raytrace sphere
	h = sphere_intersection(ro, rd, sph.xyz, sph.w, nml).x;
	if( h>0.0 && h<tmin ) {
		tmin = h;
		pos = ro + h*rd;
		nor = nml;
		matid = 3;
	}

	// raytrace torus
	h = torus_intersection(ro, rd, torus_pos, torus_params);
	if( h>0.0 && h<tmin ) {
		tmin = h;
		pos = ro + h*rd;
		nor = torus_normal(pos - torus_pos, torus_params);
		matid = 4;
	}

	// raytrace back plane
	h = plane_intersection(ro, rd, back_plane);
	if( h>0.0 && h<tmin ) {
		tmin = h;
		pos = ro + h*rd;
		nor = -back_plane.xyz;
		matid = 5;
	}

	return tmin;
}

vec2 texCoords( in vec3 pos, int mid )
{
	vec2 matuv;

	if(mid == 0) {
		matuv = pos.xz;
	} else if(mid == 5) {
		matuv = 0.25*pos.xy;
	}
	/*else if (mid==1) {
		vec3 q = normalize( pos - sc0.xyz );
		matuv = vec2( atan(q.x,q.z), acos(q.y ) )*sc0.w;
	}
	else if (mid==2) {
		vec3 q = normalize( pos - sc1.xyz );
		matuv = vec2( atan(q.x,q.z), acos(q.y ) )*sc1.w;
	}
	else if (mid==3) {
		vec3 q = normalize( pos - sc2.xyz );
		matuv = vec2( atan(q.x,q.z), acos(q.y ) )*sc2.w;
	}
	else if (mid==4) {
		vec3 q = normalize( pos - sc3.xyz );
		matuv = vec2( atan(q.x,q.z), acos(q.y ) )*sc3.w;
	}*/

	return matuv;
}

vec4 get_color( in int mid )
{
	vec4 col;

	if (mid==0)
		col = vec4(0.1, 0.1, 0.1, 0.5);
	else if (mid==1)
		col = vec4(0.9, 0.8, 0.2, 0.0);
	else if (mid==2)
		col = vec4(1.0, 0.5, 0.8, 0.0);
	else if (mid==3)
		col = vec4(0.95, 0.05, 0.05, 0.0);
	else if (mid==4)
		col = vec4(0.1, 0.1, 0.95, 0.0);
	else if (mid==5)
		col = vec4(0.1, 0.1, 0.1, 0.05);

	return col;
}

vec3 doLighting(in vec3 pos, in vec3 nor, in vec3 rd, in float illum) {
	float dif = clamp(dot(nor,light_direction),0.0,1.0);
	vec3 lin = vec3(dif * illum);
		lin += 0.1;
	return lin;
}

//===============================================================================================
//===============================================================================================
// render
//===============================================================================================
//===============================================================================================

vec3 rayTracePixel(vec3 ro, vec3 rd, out float depth)
{
	// trace
	vec3 pos, nor;
	int mid;
	float t = intersect(ro, rd, pos, nor, mid);

	vec3 col = vec3(0.9);
	if( mid != -1)
	{
		float illum = 1.0;

		if(enable_shadows) {
			vec3 spos, snor;
			int smid;
			float st = intersect(pos + 0.001*light_direction, light_direction, spos, snor, smid);
		
			if(smid != -1)
				illum = 0.0;
		}

		// calc texture sampling footprint
		vec2 uv = texCoords(pos, mid);
		
		vec4 cool = get_color(mid);

		// shading
		vec3 mate = vec3(1.0);
		mate *= gridTexture(0.9*uv);

		// lighting
		vec3 lin = doLighting(pos, nor, rd, illum);
		
		if(cool.w > 0.01)
			mate = mix(cool.xyz, mate, cool.w);
		else
			mate = cool.rgb;

		// combine lighting with material
		col = mate * lin;// * illum;
	}
	
	// gamma correction	
	col = pow( col, vec3(0.4545) );

	vec4 pos_clip = get_modelview_projection_matrix() * vec4(pos, 1.0);
	depth = 0.5*(pos_clip.z / pos_clip.w) + 0.5;
	
	// line
	return col; // * smoothstep( 1.0, 2.0, abs(fragCoord.x-iResolution.x/2.0) );
}

mat4 compute_frustum(float l, float r, float b, float t, float n, float f)
{
	mat4 M = mat4(0.0);
	M[0][0] = 2.0 * n / (r - l);
	M[2][0] = (r + l) / (r - l);
	M[1][1] = 2.0 * n / (t - b);
	M[2][1] = (t + b) / (t - b);
	M[2][2] = (n + f) / (n - f);
	M[3][2] = 2.0 * n * f / (n - f);
	M[2][3] = -1.0;
	return M;
}

// eye from left to right / -1 to 1
mat4 compute_stereo_frustum_screen(in float eye)
{
	float aspect = screen_width / screen_height;
	float top = 0.5*screen_height*z_near / parallax_zero_depth;
	float bottom = -top;
	float delta = 0.5*eye_separation*eye*screen_width*z_near / parallax_zero_depth;
	float left = bottom * aspect - delta;
	float right = top * aspect - delta;
	return compute_frustum(left, right, bottom, top, z_near, z_far);
}

// eye from left to right / -1 to 1
void stereo_translate_modelview_matrix(in float eye, in out mat4 M) {
	M[3].x += -0.5 * eye_separation * eye * screen_width;
}

float view_from_fragment_component (const vec2 frag, const int component) {
	float z = (frag.x + subpixel_width*float(component) + slope*frag.y) * pitch - center;
	z = 1. - mod(z + ceil(abs(z)), 1.);
	return z+z - 1.;
}

void main()
{
	const vec2 uv = gl_FragCoord.xy / viewport_dims;

	vec3 rgb, depth;
	for (int c=0; c<3; c++)
	{
		// determine which view the subpixel belongs to
		float view = view_from_fragment_component(uv, c);

		//view = 2.0 * test.x - 1.0;

		// transform fragment coordinates from clip coordinates to object coordinates
		//mat4 P = get_projection_matrix();
		mat4 MV = get_modelview_matrix();
		mat4 P = compute_stereo_frustum_screen(view);
		//P[0] = mix(P[0], P_[0], test.y);
		//P[1] = mix(P[1], P_[1], test.y);
		//P[2] = mix(P[2], P_[2], test.y);
		//P[3] = mix(P[3], P_[3], test.y);

		stereo_translate_modelview_matrix(view, MV);
		//MV[3].x += -0.5 * (test.x * eye_separation) * view * screen_width;

		// transform texcoord to clip coordinates
		vec4 coord = vec4(uv, 0.0, 1.0) * vec4(2.0, 2.0, 2.0, 0.0) + vec4(-1.0, -1.0, -1.0, 1.0);
		//vec4 coord = vec4(2.0 * uv - 1.0, -1.0, 1.0);

		//coord = get_inverse_modelview_projection_matrix() * coord;
		coord = inverse(P * MV) * coord;
		coord /= coord.w;

		vec3 obj_space_eye_pos = (inverse(MV) * vec4(0.0, 0.0, 0.0, 1.0)).xyz;

		vec3 ray = normalize(coord.xyz - obj_space_eye_pos);

		vec3 col = rayTracePixel(obj_space_eye_pos, ray, depth[c]);

		rgb[c] = col[c];
	}

	// FIXME: find proper way to discard subpixels (perhaps init frag color to set background color?)
	if (depth.r < 1 || depth.g < 1 || depth.b < 1)
		finish_fragment(vec4(rgb, 1.0), min(min(depth.r, depth.g), depth.b));
	else
		discard;
}
